! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine standard_l1_get_num_coarse_dofs(this, field_id, par_fe_space, num_coarse_dofs)
  implicit none
  class(standard_l1_coarse_fe_handler_t), intent(in)    :: this
  integer(ip)                           , intent(in)    :: field_id
  type(par_fe_space_t)                  , intent(in)    :: par_fe_space
  integer(ip)                           , intent(inout) :: num_coarse_dofs(:)
  
  integer(ip)                             :: ivef_within_cell
  integer(ip)                             :: ivef_within_object
  integer(ip)                             :: icell_around
  integer(ip)                             :: idof, dof_lid
  logical                                 :: dofs_on_vef
  type(environment_t), pointer            :: par_environment
  type(fe_object_iterator_t)              :: object
  type(fe_vef_iterator_t)                 :: vef
  class(fe_cell_iterator_t), allocatable  :: fe
  class(reference_fe_t), pointer          :: reference_fe
  type(list_iterator_t)                   :: own_dofs_on_vef_iterator
  integer(ip), pointer                    :: fe_dofs(:)
  logical                                 :: use_vertices, use_edges, use_faces
  type(parameterlist_t), pointer          :: parameter_list 
  
  par_environment => par_fe_space%get_environment()
  assert ( associated ( par_environment ) )
  assert ( par_environment%am_i_l1_task() )
  assert ( size(num_coarse_dofs) == par_fe_space%get_num_fe_objects() )
  
  parameter_list => this%get_parameter_list() 
  call this%get_coarse_space_use_vertices_edges_faces(parameter_list,& 
                                                      use_vertices, &
                                                      use_edges, &
                                                      use_faces)

  ! First, count how many coarse_dofs there are on the interface of my subdomain
  num_coarse_dofs = 0
  call par_fe_space%create_fe_object_iterator(object)
  call par_fe_space%create_fe_cell_iterator(fe)
  call par_fe_space%create_fe_vef_iterator(vef)
  do while ( .not. object%has_finished() )
     select case ( object%get_dim() )
     case (0)
       if (.not. use_vertices) then
         call object%next(); cycle
       end if  
     case (1)
       if (.not. use_edges) then
         call object%next(); cycle
       end if  
     case (2)
       if (.not. use_faces) then
         call object%next(); cycle
       end if  
     end select
    
     dofs_on_vef = .false.
     do ivef_within_object=1, object%get_num_vefs()
        call object%get_vef(ivef_within_object,vef)
        reference_fe => fe%get_reference_fe(field_id)
        do icell_around=1, vef%get_num_cells_around()
           call vef%get_cell_around(icell_around,fe)
           if ( fe%is_ghost() ) then 
              !Get GID of dofs on cell fe
              call fe%get_field_fe_dofs(field_id, fe_dofs)
              ivef_within_cell = fe%get_vef_lid_from_gid(vef%get_gid())

              own_dofs_on_vef_iterator = fe%create_own_dofs_on_vef_iterator(ivef_within_cell, field_id)
              do while ( .not. own_dofs_on_vef_iterator%is_upper_bound() )
                 idof    = own_dofs_on_vef_iterator%get_current()
                 dof_lid = fe_dofs(idof)
                 if ( par_fe_space%is_free_dof(dof_lid) ) then
                    dofs_on_vef = .true.
                    exit
                 end if
                 call own_dofs_on_vef_iterator%next()
              end do
           
           end if
        end do
        
        if ( dofs_on_vef ) then
           ! It must be thought for more general cases (vectors, tensor product...)
           num_coarse_dofs(object%get_gid()) = num_coarse_dofs(object%get_gid()) + reference_fe%get_num_field_components()
           exit
        end if

     end do
     call object%next()
  end do
  call par_fe_space%free_fe_vef_iterator(vef)
  call par_fe_space%free_fe_cell_iterator(fe)
  call par_fe_space%free_fe_object_iterator(object)
end subroutine standard_l1_get_num_coarse_dofs

subroutine standard_l1_setup_constraint_matrix(this, field_id, par_fe_space, constraint_matrix) 
  implicit none
  class(standard_l1_coarse_fe_handler_t), intent(in)    :: this
  integer(ip)                           , intent(in)    :: field_id
  type(par_fe_space_t)                  , intent(in)    :: par_fe_space
  type(coo_sparse_matrix_t)             , intent(inout) :: constraint_matrix
  type(environment_t), pointer :: par_environment
  type(fe_object_iterator_t) :: object
  type(fe_vef_iterator_t)                :: vef
  class(fe_cell_iterator_t), allocatable      :: fe
  class(reference_fe_t), pointer         :: reference_fe
  type(list_iterator_t)                  :: own_coarse_dofs_iterator
  type(list_iterator_t)                  :: own_dofs_on_vef_iterator 
  integer(ip), pointer                   :: fe_dofs(:)
  integer(ip)                            :: off
  integer(ip) :: istat
  integer(ip) :: num_cols
  integer(ip) :: num_fine_dofs_on_coarse_dof
  integer(ip) :: ivef_within_cell, ivef_within_object, idof, dof_lid, coarse_dof_lid, icell_around, icomp
  type(hash_table_ip_ip_t) :: visited_dofs
  
  par_environment => par_fe_space%get_environment()
  assert (associated(par_environment))
  assert (par_environment%am_i_l1_task())
  
  num_cols = par_fe_space%num_dofs_x_field(field_id)
  call visited_dofs%init(num_cols)
             
  ! Fill constraint matrix entries (transposed)
  call par_fe_space%create_fe_object_iterator(object)
  call par_fe_space%create_fe_cell_iterator(fe)
  call par_fe_space%create_fe_vef_iterator(vef)
  do while ( .not. object%has_finished() )

     if ( object%get_num_coarse_dofs(field_id) > 0 ) then

       num_fine_dofs_on_coarse_dof = 0 
       ! Count how many fine DoFs current coarse DoF aggregates
       do ivef_within_object=1, object%get_num_vefs() 
          call object%get_vef(ivef_within_object,vef)
          do icell_around=1, vef%get_num_cells_around()          
             call vef%get_cell_around(icell_around,fe)
             if ( fe%is_ghost() ) then
                call fe%get_field_fe_dofs(field_id, fe_dofs)
                ivef_within_cell = fe%get_vef_lid_from_gid(vef%get_gid())
                own_dofs_on_vef_iterator = fe%create_own_dofs_on_vef_iterator(ivef_within_cell, field_id)
                do while ( .not. own_dofs_on_vef_iterator%is_upper_bound() )
                   idof    = own_dofs_on_vef_iterator%get_current()
                   dof_lid = fe_dofs(idof)
                   if ( par_fe_space%is_free_dof(dof_lid) ) then
                      call visited_dofs%put(key=dof_lid,val=ivef_within_cell,stat=istat)
                      if ( istat == now_stored ) then
                         num_fine_dofs_on_coarse_dof = num_fine_dofs_on_coarse_dof + 1
                      end if
                   end if
                   call own_dofs_on_vef_iterator%next()
                end do
             end if
          end do
       end do
       
       own_coarse_dofs_iterator = object%create_own_coarse_dofs_iterator(field_id)
       coarse_dof_lid = own_coarse_dofs_iterator%get_current() - 1 
       call visited_dofs%init(num_cols)       
       
       do ivef_within_object=1, object%get_num_vefs()
          call object%get_vef(ivef_within_object,vef)
          do icell_around=1, vef%get_num_cells_around() 
             call vef%get_cell_around(icell_around,fe)
             if ( fe%is_ghost() ) then
                reference_fe => fe%get_reference_fe(field_id)
                assert ( own_coarse_dofs_iterator%get_size() ==  reference_fe%get_num_field_components())
                call fe%get_field_fe_dofs(field_id, fe_dofs)
                ivef_within_cell = fe%get_vef_lid_from_gid(vef%get_gid())
                own_dofs_on_vef_iterator = fe%create_own_dofs_on_vef_iterator(ivef_within_cell, field_id)
                do while ( .not. own_dofs_on_vef_iterator%is_upper_bound() )
                   idof    = own_dofs_on_vef_iterator%get_current()
                   icomp   = reference_fe%get_component_node(idof)
                   dof_lid = fe_dofs(idof)
                   if ( par_fe_space%is_free_dof(dof_lid) ) then
                      call visited_dofs%put(key=dof_lid,val=ivef_within_cell,stat=istat)
                      if ( istat == now_stored ) then
                         call constraint_matrix%insert(dof_lid, coarse_dof_lid + icomp, reference_fe%get_num_field_components()/real(num_fine_dofs_on_coarse_dof,rp))
                         !call constraint_matrix%insert(dof_lid, coarse_dof_lid, 1.0_rp/real(num_fine_dofs_on_coarse_dof,rp))
                      end if
                   end if
                   call own_dofs_on_vef_iterator%next()
                end do
             end if
          end do
       end do
     end if
     call object%next()
  end do
  call par_fe_space%free_fe_cell_iterator(fe)
  call par_fe_space%free_fe_object_iterator(object)
  call par_fe_space%free_fe_vef_iterator(vef)

end subroutine standard_l1_setup_constraint_matrix

subroutine standard_l1_setup_weighting_operator(this, field_id, par_fe_space, weighting_operator) 
  implicit none
  class(standard_l1_coarse_fe_handler_t), intent(in)    :: this
  integer(ip)                           , intent(in)    :: field_id
  type(par_fe_space_t)                  , intent(in)    :: par_fe_space
  real(rp), allocatable                 , intent(inout) :: weighting_operator(:)
  type(fe_object_iterator_t)             :: object
  type(fe_vef_iterator_t)                :: vef
  class(fe_cell_iterator_t), allocatable      :: fe
  type(list_iterator_t)                  :: own_dofs_on_vef_iterator 
  integer(ip), pointer                   :: fe_dofs(:)
  integer(ip)                            :: off
  integer(ip) :: num_fine_dofs
  integer(ip) :: num_fine_dofs_on_coarse_dof
  integer(ip) :: ivef_within_cell, ivef_within_object, idof, dof_lid, coarse_dof_lid, icell_around
  integer(ip) :: istat
  integer(ip) :: ipart, part_id, local_part_id, num_neighbours
  type(hash_table_ip_ip_t)          , allocatable :: visited_dofs(:)
  integer(ip)                       , allocatable :: num_parts_around_dof(:)
  class(triangulation_t), pointer     :: triangulation
  type(cell_import_t)               , pointer     :: cell_import

  num_fine_dofs =  size(weighting_operator)
  call memalloc (num_fine_dofs, num_parts_around_dof, __FILE__, __LINE__ )
  num_parts_around_dof  = 1

  triangulation => par_fe_space%get_triangulation()
  cell_import   => triangulation%get_cell_import()
  num_neighbours = cell_import%get_num_neighbours()
  allocate(visited_dofs(num_neighbours))
  do ipart =1,num_neighbours
     call visited_dofs(ipart)%init(num_fine_dofs)
  end do
  
  ! Fill constraint matrix entries (transposed)
  call par_fe_space%create_fe_cell_iterator(fe)
  call par_fe_space%create_fe_object_iterator(object)
  call par_fe_space%create_fe_vef_iterator(vef)
  do while ( .not. object%has_finished() )
     ! Count how many fine DoFs current coarse DoF aggregates
     do ivef_within_object=1, object%get_num_vefs()
        call object%get_vef(ivef_within_object,vef)
        do icell_around=1, vef%get_num_cells_around()  
           call vef%get_cell_around(icell_around,fe)
           if ( fe%is_ghost() ) then
              call fe%get_field_fe_dofs(field_id, fe_dofs)
              ivef_within_cell = fe%get_vef_lid_from_gid(vef%get_gid())
              own_dofs_on_vef_iterator = fe%create_own_dofs_on_vef_iterator(ivef_within_cell, field_id)
              do while ( .not. own_dofs_on_vef_iterator%is_upper_bound() )
                 idof    = own_dofs_on_vef_iterator%get_current()
                 dof_lid = fe_dofs(idof)
                 if ( par_fe_space%is_free_dof(dof_lid) ) then
                    part_id = fe%get_my_part()
                    local_part_id = cell_import%get_local_neighbour_id(part_id)
                    call visited_dofs(local_part_id)%put(key=dof_lid,val=ivef_within_cell,stat=istat)
                    if ( istat == now_stored ) then
                       num_parts_around_dof(dof_lid) = num_parts_around_dof(dof_lid) + 1
                    end if
                 end if
                 call own_dofs_on_vef_iterator%next()
              end do
           end if
        end do
     end do
     call object%next()
  end do
  call par_fe_space%free_fe_cell_iterator(fe)
  call par_fe_space%free_fe_object_iterator(object)
  call par_fe_space%free_fe_vef_iterator(vef)
  do idof =1,num_fine_dofs
     weighting_operator(idof) = weighting_operator(idof)/real(num_parts_around_dof(idof),rp)
  end do
  deallocate(visited_dofs)
  call memfree ( num_parts_around_dof, __FILE__, __LINE__ )
end subroutine standard_l1_setup_weighting_operator

subroutine standard_get_coarse_space_use_vertices_edges_faces ( parameter_list, use_vertices, use_edges, use_faces )
  implicit none
  type(parameterlist_t)                 , intent(in)    :: parameter_list
  logical                               , intent(out)   :: use_vertices, use_edges, use_faces
  integer(ip) :: FPLError
  assert(parameter_list%isAssignable(coarse_fe_handler_use_vertices_key, use_vertices))
  FPLError   = parameter_list%Get(coarse_fe_handler_use_vertices_key, use_vertices)
  assert(FPLError == 0)
  assert(parameter_list%isAssignable(coarse_fe_handler_use_edges_key, use_edges))
  FPLError   = parameter_list%Get(coarse_fe_handler_use_edges_key, use_edges)
  assert(FPLError == 0)
  assert(parameter_list%isAssignable(coarse_fe_handler_use_faces_key, use_faces))
  FPLError   = parameter_list%Get(coarse_fe_handler_use_faces_key, use_faces)
  assert(FPLError == 0)
end subroutine standard_get_coarse_space_use_vertices_edges_faces 

subroutine standard_lgt1_setup_coarse_dofs(this, coarse_fe_space) 
  implicit none
  class(standard_lgt1_coarse_fe_handler_t), intent(in)    :: this
  type(coarse_fe_space_t)                 , intent(inout) :: coarse_fe_space
end subroutine standard_lgt1_setup_coarse_dofs

!===============================================================================================
subroutine standard_l1_apply_inverse_local_change_basis(this, field_id, par_fe_space, x_old_local, x_new_local) 
  implicit none 
  class(standard_l1_coarse_fe_handler_t)   , intent(in)     :: this
  integer(ip)                              , intent(in)     :: field_id
  type(par_fe_space_t)                     , intent(in)     :: par_fe_space
  type(serial_scalar_array_t)              , intent(in)     :: x_old_local
  type(serial_scalar_array_t)              , intent(inout)  :: x_new_local
  ! Do-nothing subroutine 
end subroutine standard_l1_apply_inverse_local_change_basis

subroutine standard_l1_apply_global_change_basis(this, field_id, par_fe_space, x_new, x_old)
  implicit none 
  class(standard_l1_coarse_fe_handler_t)     , intent(inout)  :: this
  integer(ip)                                , intent(in)     :: field_id
  type(par_fe_space_t)                       , intent(in)     :: par_fe_space
  type(par_scalar_array_t)                   , intent(in)     :: x_new
  type(par_scalar_array_t)                   , intent(inout)  :: x_old
  ! Do-nothing subroutine 
end subroutine standard_l1_apply_global_change_basis

subroutine standard_l1_apply_global_change_basis_transpose(this, field_id, par_fe_space, x_old, x_new)
  implicit none 
  class(standard_l1_coarse_fe_handler_t)   , intent(in)     :: this
  integer(ip)                              , intent(in)     :: field_id
  type(par_fe_space_t)                     , intent(in)     :: par_fe_space
  type(par_scalar_array_t)                 , intent(inout)  :: x_old
  type(par_scalar_array_t)                 , intent(inout)  :: x_new
  ! Do-nothing subroutine 
end subroutine standard_l1_apply_global_change_basis_transpose

subroutine standard_l1_apply_inverse_local_change_basis_transpose(this, field_id, par_fe_space, x_new_local, x_old_local ) 
  implicit none 
  class(standard_l1_coarse_fe_handler_t)   , intent(in)     :: this
  integer(ip)                              , intent(in)     :: field_id
  type(par_fe_space_t)                     , intent(in)     :: par_fe_space
  type(serial_scalar_array_t)              , intent(in)     :: x_new_local
  type(serial_scalar_array_t)              , intent(inout)  :: x_old_local
  ! Do-nothing subroutine
end subroutine standard_l1_apply_inverse_local_change_basis_transpose

!===============================================================================================
subroutine standard_l1_free(this)
implicit none 
  class(standard_l1_coarse_fe_handler_t), intent(inout) :: this
  this%parameter_list => NULL()
end subroutine standard_l1_free
