! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine coarse_triangulation_create ( this, &
                                           environment, &
                                           num_dims, &
                                           num_local_cells, &
                                           cells_gid, &
                                           ptr_vefs_x_cell, &
                                           lst_vefs_gids, &
                                           lst_vefs_dim, &
                                           num_itfc_cells, &
                                           lst_itfc_cells, &
                                           ptr_ext_neighs_x_itfc_cell, &
                                           lst_ext_neighs_gids, &
                                           lst_ext_neighs_part_ids, &
                                           identify_disconn_components, &
                                           identify_disconn_components_dgraph_coupling )
    implicit none
    class(coarse_triangulation_t)      , intent(inout) :: this
    type(environment_t)     ,target    , intent(in)    :: environment
    integer(ip)                        , intent(in)    :: num_dims
    integer(ip)                        , intent(in)    :: num_local_cells
    integer(igp)                       , intent(in)    :: cells_gid(*)
    integer(ip)                        , intent(in)    :: ptr_vefs_x_cell(*)
    integer(igp)                       , intent(in)    :: lst_vefs_gids(*)
    integer(ip)                        , intent(in)    :: lst_vefs_dim(*)
    integer(ip)                        , intent(in)    :: num_itfc_cells
    integer(ip)                        , intent(in)    :: lst_itfc_cells(*)
    integer(ip)                        , intent(in)    :: ptr_ext_neighs_x_itfc_cell(*)
    integer(ip)                        , intent(in)    :: lst_ext_neighs_gids(*)
    integer(ip)                        , intent(in)    :: lst_ext_neighs_part_ids(*)
    logical                            , intent(in)    :: identify_disconn_components
    character(*)                       , intent(in)    :: identify_disconn_components_dgraph_coupling 
  
    integer(igp), allocatable :: lst_vefs_gids_ghost_extended(:)
    integer(ip) , allocatable :: lst_vefs_dim_ghost_extended(:)
    integer(ip) , allocatable :: cells_set(:)
    
    call this%free()
    
    !call this%set_environment(environment)
    this%environment => environment

    if(this%environment%am_i_l1_task()) then
      ! We need to fill the element_import data structure first
      ! in order to determine the number of ghost elements. This
      ! in turn is required as a precondition for the allocate_cell_array
      ! TBP below.
      call this%cell_import%create  ( this%environment%get_l1_rank()+1, &
                                         this%environment%get_l1_size(), &
                                         num_local_cells, &
                                         num_itfc_cells, &
                                         lst_itfc_cells(1:num_itfc_cells), & ! I was forced to provide l/u bounds to let gfortran 5.3.0 compile
                                         ptr_ext_neighs_x_itfc_cell(1:num_itfc_cells+1), &
                                         lst_ext_neighs_gids(1:ptr_ext_neighs_x_itfc_cell(num_itfc_cells+1)-1), &
                                         lst_ext_neighs_part_ids(1:ptr_ext_neighs_x_itfc_cell(num_itfc_cells+1)-1))
      this%num_dims  = num_dims      
      this%num_local_cells = num_local_cells
      this%num_ghost_cells = this%cell_import%get_num_ghost_elements()
      
      ! Allocate + fill cells-related data
      call this%allocate_and_fill_ptr_vefs_x_cell(ptr_vefs_x_cell)
      call this%allocate_cells_ggid()
      call this%fill_local_cells_ggid(cells_gid) 
      call this%allocate_cells_mypart()
      call this%fill_local_cells_mypart()
      call this%fetch_ghost_cells_data(lst_vefs_gids, &
                                       lst_vefs_gids_ghost_extended, &
                                       lst_vefs_dim, &
                                       lst_vefs_dim_ghost_extended)

      call this%allocate_and_fill_lst_vefs_gids(lst_vefs_gids_ghost_extended)
      
      ! Allocate + fill vefs-related data
      !call this%compute_num_local_vefs()
      !call this%compute_num_ghost_vefs()
      !this%num_vefs = this%get_num_local_vefs() + this%get_num_ghost_vefs()

      call this%allocate_and_fill_vefs_ggid(lst_vefs_gids_ghost_extended)
      call this%allocate_and_fill_vefs_dim(lst_vefs_dim_ghost_extended)
      
      call this%allocate_and_fill_cells_around()
      call this%set_up_lst_itfc_vefs()
      
      call memfree (lst_vefs_gids_ghost_extended, __FILE__, __LINE__)
      call memfree (lst_vefs_dim_ghost_extended, __FILE__, __LINE__)
      
      ! Warning: this%compute_vefs_and_parts_object() below relies on cells_set being
      !          allocated + filled. Therefore, these lines of code CANNOT be moved
      !          below the call to this%compute_vefs_and_parts_object()
      call this%allocate_cells_set()
      call memalloc(this%num_local_cells, cells_set, __FILE__, __LINE__)
      cells_set = 0
      call this%fill_cells_set(cells_set)
      call memfree (cells_set, __FILE__, __LINE__)  
    end if
    this%identify_disconn_components = identify_disconn_components
    this%identify_disconn_components_dgraph_coupling = identify_disconn_components_dgraph_coupling 
    call this%setup_coarse_triangulation()
    !call this%print()
  end subroutine coarse_triangulation_create
!  
  subroutine coarse_triangulation_allocate_and_fill_lst_vefs_gids ( this, &
                                                                    lst_vefs_gids)                                                     
    implicit none
    class(coarse_triangulation_t), intent(inout) :: this
    integer(igp)                 , intent(in)    :: lst_vefs_gids(:)
    
    type(hash_table_igp_ip_t) :: next_vef_lid_avail
    integer(ip)               :: icell, istat, j, init_pos, end_pos              

    assert ( associated ( this%environment ) )
    assert ( this%environment%am_i_l1_task() )
    assert ( this%num_local_cells + this%num_ghost_cells >=0 )

    call this%free_lst_vefs_gids()

    call memalloc ( this%ptr_vefs_x_cell(this%num_local_cells+this%num_ghost_cells+1)-1, this%lst_vefs_gids, __FILE__, __LINE__ )
    call next_vef_lid_avail%init ( max(int(real(this%ptr_vefs_x_cell(this%num_local_cells+this%num_ghost_cells+1)-1,rp)*0.1_rp),5) )
    this%num_vefs = 0
    do icell=1, this%num_local_cells + this%num_ghost_cells
      init_pos = this%ptr_vefs_x_cell(icell)
      end_pos  = this%ptr_vefs_x_cell(icell+1)-1
      do j=init_pos, end_pos
        call next_vef_lid_avail%put(key=lst_vefs_gids(j), val=this%num_vefs+1, stat=istat)
        if (istat == now_stored) then
           this%num_vefs = this%num_vefs + 1
        end if
        call next_vef_lid_avail%get(key=lst_vefs_gids(j), val=this%lst_vefs_gids(j), stat=istat)
      end do
    end do
    call next_vef_lid_avail%free()
end subroutine coarse_triangulation_allocate_and_fill_lst_vefs_gids

subroutine coarse_triangulation_allocate_and_fill_vefs_dim ( this, &
     lst_vefs_dim )
  implicit none
  class(coarse_triangulation_t), intent(inout) :: this
  integer(ip)                  , intent(in)    :: lst_vefs_dim(*)
  integer(ip) :: icell, ivef, vef_lid, current
  type(bst_cell_iterator_t) :: cell
  type(bst_vef_iterator_t)  :: vef
  
  assert ( this%num_vefs >= 0 )
  call this%free_vefs_type()
  call memalloc(this%num_vefs, this%vefs_type,__FILE__,__LINE__) 
  this%vefs_type = 0 
  
  current = 1
  call cell%create(this)
  call vef%create(this)
  do while(.not.cell%has_finished())
     do ivef=1, cell%get_num_vefs()
        vef_lid = cell%get_vef_gid(ivef)
        call vef%set_gid(vef_lid)
        call vef%set_dim(lst_vefs_dim(current))
        current = current + 1 
     end do
     call cell%next()
  end do
  call cell%free()
  call vef%free()
end subroutine coarse_triangulation_allocate_and_fill_vefs_dim


subroutine coarse_triangulation_print ( this )
   class(coarse_triangulation_t), intent(in) :: this
   integer(ip) :: ivef,inode
   type(bst_vef_iterator_t)  :: vef
   type(bst_cell_iterator_t) :: cell
   type(point_t), allocatable :: cell_coordinates(:)
   integer(ip) :: vef_lid

   if ( this%environment%am_i_l1_task() ) then
     write (*,'(a)') '****print type(coarse_triangulation_t)****'
     write (*,'(a,i10)') 'num_dims:' , this%num_dims
     write (*,'(a,i10)') 'num_local_cells:', this%num_local_cells
     write (*,'(a,i10)') 'num_ghost_cells:', this%num_ghost_cells
     write (*,'(a,i10)') 'num_itfc_vefs:  ', this%get_num_itfc_vefs()
     write (*,'(a)') '**** Cells ****'

     call cell%create(this)
     write (*,'(a)') '       id      is_local   is_ghost  num_vefs    vef        vef  ....'
     do while ( .not. cell%has_finished() )
         write(*,'(i10,2l10,10(2x,i10))') cell%get_gid(), cell%is_local(), cell%is_ghost(), cell%get_num_vefs(), (cell%get_vef_lid_from_gid(ivef),ivef=1,cell%get_num_vefs())
         call cell%next()
     end do
     call cell%free()

     write (*,'(a)') '**** Vefs ****'
     write (*,'(a)') '        id          dim    is_local    is_at_interface  is_ghost   is_at_bound '
     call vef%create(this)
     do while ( .not. vef%has_finished() )
        vef_lid = vef%get_gid()
        write(*,'(2(i10,2x),4(10x,l1),2(2x,i10))') vef_lid,  vef%get_dim(),  vef%is_local(),  vef%is_at_interface(), vef%is_ghost(), vef%is_at_boundary()
        call vef%next()
     end do
     call vef%free()
   end if
  end subroutine coarse_triangulation_print
  
  function coarse_triangulation_get_ptr_vefs_x_cell(this)
    class(coarse_triangulation_t), target, intent(in) :: this
    integer(ip), pointer :: coarse_triangulation_get_ptr_vefs_x_cell(:)
    coarse_triangulation_get_ptr_vefs_x_cell => this%ptr_vefs_x_cell 
  end function coarse_triangulation_get_ptr_vefs_x_cell
