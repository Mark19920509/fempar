! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

subroutine p4est_serial_triangulation_create (this, environment, parameters)
  implicit none
  class(p4est_serial_triangulation_t), target, intent(inout) :: this
  type(environment_t)                , target, intent(in)    :: environment
  type(ParameterList_t)                      , intent(inout) :: parameters
  integer(ip) :: istat
  type(std_vector_integer_ip_t) :: old_lst_vefs_gids
  !class(environment_t), pointer ::environment
  integer(ip) :: num_dims, idime 
  integer(ip), allocatable :: array_size(:)
  real(rp)   , allocatable :: bounding_box_limits(:)
  integer(ip) :: dummy_comm
  integer(ip) :: p4est_log_level
  integer(ip) :: geometric_interpolation_order
#ifdef ENABLE_P4EST
  call this%free()
  
  call this%set_single_octree_mesh(.true.)

  if( parameters%isPresent(triang_geometric_interpolation_order_key) ) then
     assert(parameters%isAssignable(triang_geometric_interpolation_order_key, geometric_interpolation_order))
     istat = parameters%get(key = triang_geometric_interpolation_order_key , value = geometric_interpolation_order)
     assert(istat==0)
     this%geometric_interpolation_order = geometric_interpolation_order
  else ! First order is assumed by default
     this%geometric_interpolation_order = 1
  end if
  
  call this%set_environment(environment)
  ! istat = parameters%set(key = execution_context_key, value = serial_context) ; check(istat==0)
  ! call this%allocate_environment()
  ! environment => this%get_environment()
  ! call environment%create(parameters)
  
  ! Get num_dims from FPL
  massert(parameters%isAssignable(struct_hex_triang_num_dims_key, num_dims ),'num_dims not found or un-usable.')
  istat = parameters%get(key = struct_hex_triang_num_dims_key, value = num_dims); assert(istat==0)
  call this%set_num_dims(num_dims)
  
  if( parameters%isPresent(key = struct_hex_triang_domain_limits_key) ) then
    istat = parameters%GetShape(key = struct_hex_triang_domain_limits_key, shape = array_size); check(istat==0)
    assert(array_size(1) >= 2*this%get_num_dims())
    call memalloc(array_size(1), bounding_box_limits,__FILE__,__LINE__)
    assert(parameters%isAssignable(struct_hex_triang_domain_limits_key, bounding_box_limits))
    istat = parameters%get(key = struct_hex_triang_domain_limits_key , value = bounding_box_limits); check(istat==0)
    do idime = 1,this%get_num_dims()
      this%bounding_box_limits(idime,1) = bounding_box_limits(2*idime-1)
      this%bounding_box_limits(idime,2) = bounding_box_limits(2*idime)
      assert(this%bounding_box_limits(idime,2)>this%bounding_box_limits(idime,1))
    end do
    call memfree(bounding_box_limits,__FILE__,__LINE__)
  else
    this%bounding_box_limits(:,1) = 0.0_rp
    this%bounding_box_limits(:,2) = 1.0_rp
  end if
  
  if( parameters%isPresent(key = p4est_triang_log_level_key) ) then
    assert(parameters%isAssignable(p4est_triang_log_level_key, p4est_log_level))
    istat = parameters%get(key = p4est_triang_log_level_key, value = p4est_log_level); check(istat==0)
  else
    p4est_log_level = FEMPAR_SC_LP_DEFAULT
  end if
  
  if( parameters%isPresent(key = p4est_triang_2_1_k_balance_key ) ) then
    assert(parameters%isAssignable(p4est_triang_2_1_k_balance_key, this%k_2_1_balance))
    istat = parameters%get(key = p4est_triang_2_1_k_balance_key, value = this%k_2_1_balance); check(istat==0)
  else
    this%k_2_1_balance = default_p4est_triang_2_1_k_balance
  end if

  if( parameters%isPresent(key = p4est_triang_k_ghost_cells_key ) ) then
    assert(parameters%isAssignable(p4est_triang_k_ghost_cells_key, this%k_ghost_cells))
    istat = parameters%get(key = p4est_triang_k_ghost_cells_key, value = this%k_ghost_cells); check(istat==0)
  else
    this%k_ghost_cells = default_p4est_triang_k_ghost_cells
  end if
  
  call this%reference_fe_geo%create( topology_hex, &
                                     this%get_num_dims(), &
                                     1, &
                                     field_type_scalar, & 
                                     conformity=.true.)  
 
  call this%allocate_and_gen_reference_fe_geo_scratch_data()
  
  dummy_comm = 0
  call F90_p4est_init(dummy_comm,p4est_log_level)
  if ( this%get_num_dims() == 2 ) then
    call F90_p4est_connectivity_new_unitsquare(this%p4est_connectivity)
    call F90_p4est_connectivity_set_bounding_box_limits(this%p4est_connectivity, c_loc(this%bounding_box_limits) )
    call F90_p4est_new(dummy_comm,this%p4est_connectivity, this%p4est)
    call this%update_p4est_mesh()
    call this%update_topology_from_p4est_mesh()
    call this%update_cell_import()
    call this%update_cell_ggids()
    call this%update_cell_myparts()
    call this%extend_p4est_topology_arrays_to_ghost_cells()
    call this%update_lst_vefs_gids_and_cells_around(old_lst_vefs_gids)
    
    !call this%fill_x_cell_nodal_coordinates()
    
    call old_lst_vefs_gids%free()
    call this%clear_refinement_and_coarsening_flags()
    call this%clear_cell_weights()
    call this%clear_cell_set_ids()
    call this%set_up_lst_itfc_vefs()
    call this%clear_vef_set_ids()
  else if ( this%get_num_dims() == 3 ) then
    call F90_p8est_connectivity_new_unitcube(this%p4est_connectivity)
    call F90_p8est_new(dummy_comm,this%p4est_connectivity, this%p4est)
    call F90_p4est_connectivity_set_bounding_box_limits(this%p4est_connectivity, c_loc(this%bounding_box_limits) )
    call this%update_p4est_mesh()
    call this%update_topology_from_p4est_mesh()
    call this%update_cell_import()
    call this%update_cell_ggids()
    call this%update_cell_myparts()
    call this%extend_p4est_topology_arrays_to_ghost_cells()
    call this%find_missing_corner_neighbours()
    call this%update_lst_vefs_gids_and_cells_around(old_lst_vefs_gids)
    
    !call this%fill_x_cell_nodal_coordinates()
    
    call old_lst_vefs_gids%free()
    call this%clear_refinement_and_coarsening_flags()
    call this%clear_cell_weights()
    call this%clear_cell_set_ids()
    call this%set_up_lst_itfc_vefs()
    call this%clear_vef_set_ids()
    ! IMPORTANT NOTE: we need to RE-SETUP lst_itfc_vefs as we may have added 
    ! new proper interface VEFs in update_lst_vef_gids_and_cells_around_ghost_cells()
    call this%set_up_lst_itfc_vefs()
  end if
  allocate ( serial_fe_space_t :: this%nodal_coordinates_fe_space )
  call this%setup_workspace_to_compute_nodal_coordinates()
  call this%fill_nodal_coordinates_fe_function()
#else
  call this%not_enabled_error()
#endif
end subroutine p4est_serial_triangulation_create  

subroutine p4est_serial_triangulation_free ( this)
  implicit none
  class(p4est_serial_triangulation_t), target, intent(inout) :: this
  type(cell_import_t), pointer :: cell_import
  
#ifdef ENABLE_P4EST  
  if ( this%get_num_dims() == 2 ) then
    call F90_p4est_destroy(this%p4est)
    call F90_p4est_connectivity_destroy(this%p4est_connectivity)
    call F90_p4est_mesh_destroy(this%p4est_mesh)
    call F90_p4est_ghost_destroy(this%p4est_ghost)
  else if ( this%get_num_dims() == 3 ) then  
    call F90_p8est_destroy(this%p4est)
    call F90_p8est_connectivity_destroy(this%p4est_connectivity)
    call F90_p8est_mesh_destroy(this%p4est_mesh)
    call F90_p4est_locidx_buffer_destroy(this%QHE)
    call F90_p8est_ghost_destroy(this%p4est_ghost)
  end if
  
  this%p4est_connectivity = c_null_ptr
  this%p4est              = c_null_ptr
  this%p4est_mesh         = c_null_ptr
  this%QHE                = c_null_ptr
  this%p4est_ghost        = c_null_ptr
    
  call this%lst_vefs_gids%free()
  call this%cell_ggids%free()
  call this%ptr_cells_around_proper_vefs%free()
  call this%lst_cells_around_proper_vefs%free()
  call this%ptr_cells_around_improper_vefs%free()
  call this%lst_cells_around_improper_vefs%free()
  call this%ptr_improper_cells_around%free()
  call this%lst_improper_cells_around%free()
  call this%improper_vefs_improper_cell_around_ivef%free()
  call this%improper_vefs_improper_cell_around_subvef%free()
  call this%proper_vefs_dim%free()
  call this%improper_vefs_dim%free()
  call this%proper_vefs_at_boundary%free()
  call this%proper_vefs_at_interface%free()
  call this%improper_vefs_at_interface%free()
  call this%refinement_and_coarsening_flags%free()
  call this%cell_set_ids%free()
  call this%cell_weights%free()
  call this%proper_vefs_set_ids%free()
  call this%improper_vefs_set_ids%free()
  call this%free_lst_itfc_vefs()
  
  cell_import => this%get_cell_import()
  call cell_import%free()
  
  if (associated(this%quad_to_quad))   deallocate(this%quad_to_quad)
  if (associated(this%quad_to_face))   deallocate(this%quad_to_face)
  if (associated(this%quad_to_half))   deallocate(this%quad_to_half)
  if (associated(this%quad_to_corner)) deallocate(this%quad_to_corner)
  if (associated(this%quad_to_half_by_edge) ) deallocate(this%quad_to_half_by_edge)
  
  if (allocated(this%quad_coords)) &
     call memfree(this%quad_coords, __FILE__, __LINE__)
  
  if (allocated(this%quad_level)) &
    call memfree(this%quad_level, __FILE__, __LINE__)

  if (allocated(this%quad_to_quad_by_edge)) &
    call memfree(this%quad_to_quad_by_edge, __FILE__, __LINE__)

  if (allocated(this%quad_to_edge)) &
    call memfree(this%quad_to_edge, __FILE__, __LINE__)
    
  if (allocated(this%global_first_quadrant)) &
    call memfree(this%global_first_quadrant, __FILE__, __LINE__)
  
  this%k_2_1_balance  = -1
  this%k_ghost_cells = -1
  this%num_proper_vefs = -1
  this%num_improper_vefs = -1
  this%bounding_box_limits = 0.0_rp
  this%previous_num_local_cells = -1
  this%previous_num_ghost_cells = -1
  
  this%bounding_box_limits = 0.0_rp

  call this%reference_fe_geo%free()
  call this%free_reference_fe_geo_scratch_data()
  !call this%free_environment()
  
  this%clear_refinement_and_coarsening_flags_pending = .false.
  
  ! Free nodal coordinates workspace
  call this%free_workspace_to_compute_nodal_coordinates()
  
  call triangulation_free(this)
#else
  call this%not_enabled_error()
#endif     
end subroutine p4est_serial_triangulation_free
